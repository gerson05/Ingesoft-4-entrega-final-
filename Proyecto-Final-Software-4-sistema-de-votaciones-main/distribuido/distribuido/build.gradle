plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.5.0' apply false
    id 'com.github.johnrengelman.shadow' version '7.1.1' apply false
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'
    apply plugin: 'com.github.johnrengelman.shadow'

    slice {
        java {
            files = [file("../Printer.ice")]
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'com.zeroc:ice:3.7.2'
        implementation 'org.apache.poi:poi:5.0.0'
        implementation 'org.apache.poi:poi-ooxml:5.0.0'
        implementation 'org.postgresql:postgresql:42.6.0'
    }

    tasks.jar.enabled = false // Deshabilitamos la tarea jar estándar

    shadowJar {
        archiveBaseName.set(project.name)
        archiveClassifier.set('') // Esto asegura que sea el JAR principal
        archiveVersion.set('') // Sin versión en el nombre del JAR
        manifest {
            attributes(
                    "Main-Class": project.name.capitalize()
            )
        }
    }

    tasks.build {
        dependsOn shadowJar // Aseguramos que shadowJar se ejecute al construir
    }
}